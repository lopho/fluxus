###############################################################
# SConscript for fluxus-midi
#
# This is the main RACKET module for the fluxus midi functions

Import("env", "BinaryModulesLocation", "MZDYN", "static_modules", "racket_framework")

if env['PLATFORM'] == 'darwin':
	Target = "fluxus-midi_ss.dylib"
else:
	Target = "fluxus-midi_ss.so"

static_modules = False
if static_modules: Target = "fluxus-midi_ss"

Install = BinaryModulesLocation

Source = ["src/FluxusMIDI.cpp",
		"src/MIDIListener.cpp",
		"src/MIDIOut.cpp",
		"src/RtMidi.cpp",
		MZDYN]

if env['PLATFORM'] == 'posix':
	Libs =  ['pthread', 'asound']
	Frameworks = []
	env.Append(CCFLAGS = ' -D__LINUX_ALSASEQ__')
elif env['PLATFORM'] == 'darwin':
	Libs = []
	Frameworks = ['CoreMIDI', 'CoreAudio', 'CoreFoundation']
	if racket_framework:
		Frameworks += ['Racket']
	else:
		Libs += ['racket3m']

	env.Append(CCFLAGS = ' -D__MACOSX_CORE__')

if static_modules:
	env.StaticLibrary(source = Source,
				  target = Target,
				  FRAMEWORKS = Frameworks,
				  LIBS = Libs)
else:
	env.SharedLibrary(source = Source,
				  target = Target,
				  LIBS = Libs,
				  FRAMEWORKS = Frameworks,
				  SHLIBPREFIX = "")

if not static_modules: env.Install(Install, Target)
